plugins {
    id 'java'
    id 'antlr'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.9.3"
    implementation "org.antlr:antlr4-runtime:4.9.3"
    implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore', version: '2.35.0'
    implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore.xmi', version: '2.36.0'
    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'aam65.j2ecore'
            artifactId = 'j2ecore'
            version = '0.1.0'

            artifact sourceJar
        }
    }
    repositories {
        mavenLocal()
    }
}

shadowJar {
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'aam65.j2ecore.Main'
    }

    // Exclude signature files that can cause a SecurityException when running the JAR.
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"

    // Merge the service provider configurations.
    mergeServiceFiles()

}


